#!/usr/bin/env zsh

zparseopts -D -- P+:=abs_paths p+:=rel_paths f+:=env_files s+:=env_vars c+:=cmds || exit

for p in ${(@)abs_paths:#-P}; do
    prefixes=($p $prefixes)
done
for p in ${(@)rel_paths:#-p}; do
    for b in ${(s.:.)PENVPATH}; do
        if [[ -e "$b/$p" ]] then
            prefixes=("$b/$p" $prefixes)
            break
        fi
    done
done

bin_path=("$PATH")
library_path=("$LD_LIBRARY_PATH")
include_path=("$CPPFLAGS")
linker_path=("$LDFLAGS")
man_path=("$MANPATH")

for p in ${(@)prefixes}; do
    [[ -e "$p/bin" ]]       && bin_path=("$p/bin" $bin_path)
    [[ -e "$p/sbin" ]]      && bin_path=("$p/sbin" $bin_path)
    [[ -e "$p/include" ]]   && include_path=("-I$p/include" $include_path)
    [[ -e "$p/share/man" ]] && man_path=("$p/share/man" $man_path)
    [[ -e "$p/lib" ]]       && library_path=("$p/lib" $library_path)
    [[ -e "$p/lib" ]]       && linker_path=("-L$p/lib" $linker_path)
    [[ -e "$p/lib" ]]       && cflags=("-Wl,-rpath=$p/lib" $cflags)
done

for f in ${(@)env_files:#-f}; do
    source $f
done

for s in ${(@)env_vars:#-s}; do
    var=("$(env \
            PATH="${(j.:.)bin_path}" \
            LD_LIBRARY_PATH="${(j.:.)library_path}" \
            LD_RUN_PATH="${(j.:.)library_path}" \
            CPPFLAGS="${(j. .)include_path}" \
            LDFLAGS="${(j. .)linker_path}" \
            MAN_PATH="${(j.:.)man_path}" \
            CFLAGS="${(j.:.)cflags}" \
            ${(@)vars} \
            envsubst <<< $s)")
    vars=($vars "$(env \
                   PATH="${(j.:.)bin_path}" \
                   LD_LIBRARY_PATH="${(j.:.)library_path}" \
                   LD_RUN_PATH="${(j.:.)library_path}" \
                   CPPFLAGS="${(j. .)include_path}" \
                   LDFLAGS="${(j. .)linker_path}" \
                   MAN_PATH="${(j.:.)man_path}" \
                   CFLAGS="${(j.:.)cflags}" \
                   ${(@)vars} \
                   zsh -c "eval echo $s")")
done

for c in ${(@)cmds:#-c}; do
    env \
        PREFIXES="${(j.:.)prefixes}" \
        PATH="${(j.:.)bin_path}" \
        LD_LIBRARY_PATH="${(j.:.)library_path}" \
        LD_RUN_PATH="${(j.:.)library_path}" \
        CPPFLAGS="${(j. .)include_path}" \
        LDFLAGS="${(j. .)linker_path}" \
        MAN_PATH="${(j.:.)man_path}" \
        CFLAGS="${(j.:.)cflags}" \
        ${(@)vars} \
        $SHELL -c "$c"
done

if [[ -z "$cmds" ]]; then
    exec env \
        PREFIXES="${(j.:.)prefixes}" \
        PATH="${(j.:.)bin_path}" \
        LD_LIBRARY_PATH="${(j.:.)library_path}" \
        LD_RUN_PATH="${(j.:.)library_path}" \
        CPPFLAGS="${(j. .)include_path}" \
        LDFLAGS="${(j. .)linker_path}" \
        MAN_PATH="${(j.:.)man_path}" \
        CFLAGS="${(j.:.)cflags}" \
        ${(@)vars} \
        "$@"
fi
